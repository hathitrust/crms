#!/usr/bin/perl

use strict;
use utf8;

BEGIN {
  die "SDRROOT environment variable not set" unless defined $ENV{'SDRROOT'};
  use lib $ENV{'SDRROOT'} . '/crms/cgi';
}

use CGI;
use Encode;
use POSIX;
use Template;
use URI::Escape;

use CRMS;

binmode(STDOUT, ':encoding(UTF-8)');
my $cgi = new CGI;
my $page = $cgi->param('p');
my $debugSql = $cgi->param('debugSql');
my $crms = CRMS->new(
        cgi       => $cgi,
        verbose   => 0,
        debugSql  => $debugSql
        );
my $redirect;
my $uri = $ENV{'REQUEST_URI'};
$uri =~ s/&/;/g;
my $target = uri_escape_utf8('https://'. $ENV{'HTTP_HOST'}. $uri);
# remote_user is the crms.users primary key
# If not set, may be authenticated with ht.ht_users but not in the CRMS system
if (!$crms->get('remote_user'))
{
  $redirect = 'https://'. $ENV{'HTTP_HOST'}. "/cgi/wayf?target=$target";
}
if ($crms->get('stepup') && $crms->get('stepup_redirect'))
{
  $redirect = $crms->get('stepup_redirect');
}

# If $redirect is set then the user is unauthenticated
# Emit the redirect immediately and terminate
if ($redirect) {
  print $cgi->redirect($redirect);
  exit;
}

if ($page eq 'Logout')
{
  # Remove all locks for this user.
  # If we're aliased, we don't log out, we just drop the alias.
  my $alias = $crms->GetAlias();
  if ($alias)
  {
    $crms->UnlockAllItemsForUser($alias);
    $crms->SetAlias();
    $cgi->delete_all();
  }
  else
  {
    $crms->UnlockAllItemsForUser();
    $target =~ s/logout/home/gi;
    $redirect = 'https://' . $ENV{'HTTP_HOST'} . '/cgi/logout?' . $target;
    print $cgi->redirect($redirect);
    exit;
  }
}
elsif ($cgi->param('changeuser') == 1)
{
  $crms->SetAlias(undef, $cgi->param('newuser'));
  $cgi->delete_all();
}

my $user = $crms->get('user');

### Review submission: all projects go through here.
if ($page eq 'finishReview')
{
  my $editing = $cgi->param('editing');
  my $id = $cgi->param('htid');
  my $err;
  my $action = $cgi->param('submit');
  if ($action eq 'Cancel' || !$action)
  {
    $crms->UnlockItem($id, $user);
    $cgi->delete_all();
  }
  else
  {
    my $stat = $crms->GetSystemStatus();
    my $status = $stat->[1];
    if ($status ne 'normal')
    {
      $err = "CRMS is not currently accepting reviews (status '$status'). Please Cancel.";
    }
    if (!$err)
    {
      $err = $crms->SubmitReviewCGI($id, $user, $cgi);
    }
    if ($err)
    {
      $crms->ClearErrors();
      $cgi->param('errorMsg', $err);
      $cgi->param('p', 'review');
    }
    else
    {
      $cgi->delete_all();
      $cgi->param('p', 'review');
    }
  }
  if ($editing && !$err)
  {
    print "Content-type: text/html\n\n";
    print '<script type="text/javascript">window.close();</script>';
    exit(0);
  }
}

if ($cgi->param('download'))
{
  use Downloads;
  Downloads::Download($crms, $cgi);
  exit(0);
}

## the template page (tt) is called based on the CGI 'p' param (ex editReviews -> editReviews.tt)
## the default is home.tt in case a tt is not found
$page = $cgi->param('p') || 'home';
if (!$crms->GetAlias())
{
  my %ips = %{$crms->GetUserIPs()};
  if (scalar keys %ips)
  {
    my $ip = $ENV{'REMOTE_ADDR'};
    if (!defined $ips{$ip} &&
        !defined $ips{'mfa'} &&
        ($crms->IsUserReviewer() ||
         $crms->IsUserAdvanced() ||
         $crms->IsUserExpert()))
    {
      $cgi->param('REMOTE_ADDR', $ip);
      $page = 'ip';
    }
  }
}
my $exp = $crms->IsUserExpired($user);
$page = 'expired' if $exp->{'status'} == 1;
if (length $page)
{
  my $dbc = $crms->AccessCheck($page);
  if (defined $dbc)
  {
    $page = 'denied';
    $cgi->param('dbc_err', $dbc->{'err'});
    $cgi->param('dbc_page', $dbc->{'page'});
  }
}

my $input = $page . '.tt';
$input = 'home.tt' unless -f $input;

## config options for TT
my $config =
{
    INCLUDE_PATH => '.',
    INTERPOLATE  => 1,     ## expand "$var" in plain text
    POST_CHOMP   => 1,     ## cleanup whitespace
    EVAL_PERL    => 1,     ## evaluate Perl code blocks
    ENCODING     => 'UTF-8'
};

my $tt   = Template->new($config);
my $vars = {
    title     => 'CRMS',
    crms      => $crms,
    cgi       => $cgi,
    user      => $user,
    project   => $crms->GetUserCurrentProject(),
    env       => $ENV,
    yesterday => POSIX::strftime("%Y-%m-%d %H:%M:%S", localtime(time-86400))
};

print $cgi->header(-charset => 'utf-8', 'cache-control' => 'private');
if (!$tt->process($input, $vars))
{
  printf "<h3>%s</h3>\n", $tt->error();
  die $tt->error();
}

# Debugging output is restricted to development instance
exit if CRMS::Config->new->instance ne 'development';

# Print accumulated output of DebugSql calls
print $crms->Debug();

# Print diagnostics with debug URL parameter
if ($cgi->param('debug'))
{
  print "<div class=\"debug-footer\">\n<h3>Debug Information</h3>\n";
  eval {
    use Data::Dumper;

    printf 'Host: <b>%s</b><br/>', `hostname`;
    my $dbinfo = $crms->DbInfo();
    $dbinfo =~ s/\n/<br\/>/g;
    print "<i>$dbinfo</i><br/>\n";
    print '<br/><strong>Environment Variables:</strong><br/>';
    foreach my $k (sort keys %ENV)
    {
      printf "&nbsp;&nbsp;&nbsp;$k: %s<br/>\n", $ENV{$k};
    }
    print "<br/><strong>Perl ${^V}  Include Paths:</strong><br/>";
    foreach my $inc (sort @INC)
    {
      print "&nbsp;&nbsp;&nbsp;$inc<br/>\n";
    }
    print "<br/><strong>CRMS Variables:</strong><br/>\n";
    foreach my $k (sort keys %{$crms})
    {
      my $value = $crms->get($k);
      $value = Dumper($value) if ref($value) ne '';
      printf "&nbsp;&nbsp;&nbsp;$k: <pre class=\"perl-data\">%s</pre><br/>\n", $value;
    }
  };
  print "</div>\n";
  print "<i>Error printing debug information:</i> <code>$@</code>\n" if $@;
}

